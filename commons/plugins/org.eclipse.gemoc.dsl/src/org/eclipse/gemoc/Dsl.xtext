grammar org.eclipse.gemoc.Dsl hidden() 

generate dsl "http://www.eclipse.org/gemoc/Dsl" 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Dsl :
	{Dsl}
	(
		(
			entries+=Entry |
			=> EMPTY_LINE | 
			SL_COMMENT 
		) ('\r'? '\n')*
	)*
;

Entry:
	WS* key=ENTRYKEY WS* SEPARATOR WS* value=EntryValue 
;

EntryValue:
	{EntryValue}
	entryLines+=ENTRYVALUE_LINE 
	(
		WS*
		(
			(=> (',' WS* '\\' LINE_START) entryLines+=ENTRYVALUE_LINE) |
			(=> ('\\' LINE_START) entryLines+=ENTRYVALUE_LINE) |
			(',' WS* entryLines+=ENTRYVALUE_LINE)
		) 
	)* 
;
ENTRYVALUE_LINE:
	(ENTRYKEY | STRING) 
;

EMPTY_LINE:
	LINE_START 
;

terminal ENTRYKEY:
	('a'..'z'|'A'..'Z'|'_'|'.'|'0'..'9')('a'..'z'|'A'..'Z'|'_'|'.'|'0'..'9')*
;

//terminal END: EOF;

terminal STRING: 
   // '"' ( '\\'('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"';
    '"' ( '\\'('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\'|'\r'|'\n') | !('\\'|'"'|'\r'|'\n') )* '"';
    
terminal LINE_START:
	'\r'? '\n' WS*
;    

terminal SEPARATOR:
	'=' | ':'
;

/*
 * Line starting with '!' or '#'
 */
terminal SL_COMMENT :
	('!' | '#') !('\n'|'\r')* 
;


terminal WS:
	' '|'\t'
;


terminal ANY_OTHER:
    .;

